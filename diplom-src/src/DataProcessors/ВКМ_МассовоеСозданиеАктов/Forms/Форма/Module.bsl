//НачалоДоработки. Контеева, 27.05.24. 
//Массовое создание реализации за прошлый месяц на все договора обслуживания (абонентская плата и выполненные работы)
//через Длительные операции

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	ПередЗакрытиемНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриОткрытииНаСервере();
КонецПроцедуры   

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Заполнить(Команда)
	
	ДлительнаяОперация = ЗаполнитьНаСервере(Объект.Период);
	
	Если ЗначениеЗаполнено(Объект.Период) Тогда 
		
		Оповещение = Новый ОписаниеОповещения ("ВыполнитьВФонеЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение);  
		
	Иначе 
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Заполните период оформления реализаций";
		Сообщение.Сообщить();  
		
	КонецЕсли;  
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Функция ЗаполнитьНаСервере(Период) 
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор); 	                    
	Результат = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ВКМ_МассовоеСозданиеАктов.ТяжелаяОперацияСозданиеРеализаций", Период); 
	Возврат Результат;
	
КонецФункции  

&НаКлиенте 
Процедура ВыполнитьВФонеЗавершение (Результат, Доппараметры) Экспорт 
	
	ВыполнитьВФонеЗавершениеСервер (Результат, Доппараметры);  
	
КонецПроцедуры  

&НаСервере 
Процедура ВыполнитьВФонеЗавершениеСервер (Результат, Доппараметры) Экспорт 
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Хранилище = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	ТЗ = Хранилище.Получить();
	Для каждого ЭлементМассива из ТЗ Цикл
		НоваяСтрока = Объект.СозданныеДокументы.Добавить();   
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементМассива);    
		
	КонецЦикла;  
	
КонецПроцедуры

&НаСервере
Процедура ПередЗакрытиемНаСервере()
	СохранитьНастройки();
КонецПроцедуры  

&НаСервере
Процедура СохранитьНастройки()
	
	КлючНастроек = "СписокДокументов";    
	Настройки = Новый Соответствие;
	
	Настройки.Вставить("СозданныеДокументы", Объект.СозданныеДокументы.Выгрузить());
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("СписокДокументов", КлючНастроек, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура ПриОткрытииНаСервере()
	ВосстановитьНастройки();
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройки()
	
	КлючНастроек = "СписокДокументов";    
	ЗначениеНастроек = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("СписокДокументов", КлючНастроек);    
	Если ТипЗнч(ЗначениеНастроек) = Тип("Соответствие") Тогда
		Объект.СозданныеДокументы.Загрузить(ЗначениеНастроек.Получить("СозданныеДокументы"));    
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

