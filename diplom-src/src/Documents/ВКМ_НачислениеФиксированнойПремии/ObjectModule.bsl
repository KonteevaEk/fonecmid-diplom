//НачалоДоработки. Контеева, 06.05.24. 
//Начисление премии

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	#Область ОбработчикиСобытий
	
	Процедура ОбработкаПроведения(Отказ, Режим)
		
		//Движения.ВКМ_ДополнительныеНачисления.Записывать = Истина;
		
		Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;  
		Движения.ВКМ_ВзаиморасчетыССотрудниками.Очистить(); 
		
		СформироватьДвиженияДопНачисления();   
		СформироватьДвиженияУдержания();
		
		РассчитатьНФДЛ();
		
	КонецПроцедуры
	
	#КонецОбласти
	
	#Область СлужебныеПроцедурыИФункции
	
	Процедура СформироватьДвиженияДопНачисления()
		
		Для Каждого Строка Из СписокСотрудников Цикл  
			
			// регистр ВКМ_ДополнительныеНачисления
			Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить(); 
			Движение.ПериодРегистрации = Дата; 
			Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.Премия;
			Движение.Сотрудник = Строка.Сотрудник;
			Движение.Начислено = Строка.СуммаПремии;    
			
			// регистр ВКМ_ВзаиморасчетыССотрудниками Приход
			ДвижениеВз = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
			ДвижениеВз.ВидДвижения = ВидДвиженияНакопления.Приход;
			ДвижениеВз.Период = Дата;
			ДвижениеВз.Сотрудник = Строка.Сотрудник;
			ДвижениеВз.Сумма = Движение.Начислено;   
			
		КонецЦикла;
		
		Движения.ВКМ_ДополнительныеНачисления.Записать();
		
	КонецПроцедуры 
	
	Процедура СформироватьДвиженияУдержания()
		
		Для Каждого Строка Из СписокСотрудников Цикл   
			
			// регистр ВКМ_Удержания	
			Движение = Движения.ВКМ_Удержания.Добавить();
			Движение.ПериодРегистрации = Дата;
			Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
			Движение.Сотрудник = Строка.Сотрудник;
			Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
			Движение.БазовыйПериодКонец = КонецМесяца(Дата);  
			
		КонецЦикла;
		
		Движения.ВКМ_Удержания.Записать();
		
	КонецПроцедуры 
	
	Процедура РассчитатьНФДЛ()
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_Удержания.НомерСтроки КАК НомерСтроки,
		|	ВКМ_Удержания.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.НачисленоБаза, 0) КАК НачисленоБаза,
		|	ВКМ_Удержания.БазовыйПериодКонец КАК БазовыйПериодКонец,
		|	ВКМ_Удержания.БазовыйПериодНачало КАК БазовыйПериодНачало,
		|	ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало1,
		|	ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.БазовыйПериодКонец КАК БазовыйПериодКонец1,
		|	ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.РегистраторРазрез КАК РегистраторРазрез
		|ИЗ
		|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_Удержания.БазаВКМ_ДополнительныеНачисления(
		|				&Измерения,
		|				&Измерения,
		|				&Разрезы,
		|				Регистратор = &Ссылка
		|					И ВидРасчета = &Удержание) КАК ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления
		|		ПО ВКМ_Удержания.НомерСтроки = ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.НомерСтроки
		|ГДЕ
		|	ВКМ_Удержания.Регистратор = &Ссылка
		|	И ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.РегистраторРазрез = ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.Регистратор";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);   
		Запрос.УстановитьПараметр("Удержание",  ПланыВидовРасчета.ВКМ_Удержания.НДФЛ);  
		
		Измерения = Новый Массив;
		Измерения.Добавить("Сотрудник");
		
		Разрезы = Новый Массив;
		Разрезы.Добавить("Регистратор");
		
		Запрос.УстановитьПараметр("Измерения", Измерения); 
		Запрос.УстановитьПараметр("Разрезы", Разрезы);  
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			// регистр ВКМ_ДополнительныеНачисления
			ДвижениеУдержание = Движения.ВКМ_Удержания[Выборка.НомерСтроки-1];   
			ДвижениеУдержание.НДФЛ = Выборка.НачисленоБаза/100*13; 
			Движения.ВКМ_Удержания.Записать(, Истина);
			
			// регистр ВКМ_ВзаиморасчетыССотрудниками Расход
			ДвижениеВз = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
			ДвижениеВз.ВидДвижения = ВидДвиженияНакопления.Расход;
			ДвижениеВз.Период = Дата;
			ДвижениеВз.Сотрудник = Выборка.Сотрудник;
			ДвижениеВз.Сумма = ДвижениеУдержание.НДФЛ;   
			
		КонецЦикла;  
		
	КонецПроцедуры
	
	#КонецОбласти
	
#КонецЕсли

//Конец доработки